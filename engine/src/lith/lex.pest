// Whitespace, comments ////////////////////////////////////////////////////////

	WHITESPACE = _{ " " | "\t" | NEWLINE }
	COMMENT = { BlockComment | LineComment }

	LineComment = { "//" ~ (!("/" | "!") | "//") ~ "\n"* | "//" }
	BlockComment = {
		"/*" ~ (!("*" | "!") | "**" | BlockCommentOrDoc) ~
		(BlockCommentOrDoc | (!"*/" ~ ANY))* ~
		"*/" | "/**/" | "/***/"
	}

	BlockCommentOrDoc = { BlockComment | OuterBlockDoc | InnerBlockDoc }
	IsolatedCr = { "\r" ~ (!"\n" ~ ANY) }

	InnerLineDoc = { "//!" ~ !("\n" | IsolatedCr)* }
	InnerBlockDoc = {
		"/*!" ~ (BlockCommentOrDoc | (!("*/" | IsolatedCr) ~ ANY))* ~ "*/"
	}
	OuterLineDoc = { "///" ~ (!"/" ~ !("\n" | IsolatedCr)*)? }
	OuterBlockDoc = {
		"/**" ~
		(!"*" | BlockCommentOrDoc) ~
		(BlockCommentOrDoc | (!("*/" | IsolatedCr) ~ ANY))* ~
		"*/"
	}

// Top-level ///////////////////////////////////////////////////////////////////

	TokenStream = { SOI ~ Token* ~ EOI }

	Token = _{ Literal | Keyword | Ident | Glyph }

// Literals ////////////////////////////////////////////////////////////////////

	Literal = _{ NullLit | BoolLit | NumLit | CharLit | StringLit }

	NullLit = { "null" }
	BoolLit = { "true" | "false" }

	QuoteEscape = { "\\'" | "\\\"" }
	AsciiEscape = @{
		("\\x" ~ ASCII_OCT_DIGIT ~ ASCII_HEX_DIGIT) |
		"\\n" | "\\r" | "\\t" | "\\\\" | "\\0"
	}
	UnicodeEscape = @{ "\\u" ~ (ASCII_HEX_DIGIT ~ "_"*){1,6} }
	CharLit = @{
		"'" ~
		(!("'" | "\\" | "\n" | "\r" | "\t") ~ ANY
		| QuoteEscape | AsciiEscape | UnicodeEscape) ~
		"'"
	}

	StringLit = @{
		"\"" ~ (
			!("\"" | "\\" | IsolatedCr) ~
			(ANY | QuoteEscape | AsciiEscape | UnicodeEscape)
		)* ~ "\""
	}

	NumLit = _{ IntLit | FloatLit }
	IntLit = @{ (DecLit | BinLit | OctLit | HexLit) ~ IntSuffix? }
	IntSuffix = {
		"i8" | "i16" | "i32" | "i64" | "i128" |
		"u8" | "u16" | "u32" | "u64" | "u128"
	}
	DecLit = @{ ASCII_DIGIT ~ (ASCII_DIGIT | "_")* }
	BinLit = @{
		"0b" ~
		(ASCII_BIN_DIGIT | "_")* ~
		ASCII_BIN_DIGIT ~
		(ASCII_BIN_DIGIT | "_")*
	}
	OctLit = @{
		"0o" ~
		(ASCII_OCT_DIGIT | "_")* ~
		ASCII_OCT_DIGIT ~
		(ASCII_OCT_DIGIT | "_")*
	}
	HexLit = @{
		"0x" ~
		(ASCII_HEX_DIGIT | "_")* ~
		ASCII_HEX_DIGIT ~
		(ASCII_HEX_DIGIT | "_")*
	}

	FloatLit = @{
		DecLit ~ "." ~ FloatPointFollow |
		DecLit ~ FloatExponent |
		DecLit ~ "." ~ DecLit ~ FloatExponent? |
		DecLit ~ ("." ~ DecLit)? ~ FloatExponent? ~ FloatSuffix
	}
	FloatPointFollow = _{ !("." | "_" | XID_START) ~ ANY }
	FloatExponent = @{
		^"e" ~ ("+" | "-")? ~
		(ASCII_DIGIT | "_")* ~
		ASCII_DIGIT ~
		(ASCII_DIGIT | "_")*
	}
	FloatSuffix = { "f32" | "f64" }

	RawString = @{ "r" ~ PUSH("#"*) ~ "\"" ~ RawStringInner ~ "\"" ~ POP }
	RawStringInner = @{ (!("\"" ~ PEEK) ~ ANY)* }

// Keywords ////////////////////////////////////////////////////////////////////

	Keyword = _{
		Abstract | Break | Catch | Ceval | Class | Const | Continue | Defer |
		Do | Else | Enum | Final | Finally | For | ForEach | If | In | Interface |
		Is | Let | Loop | Macro | Mixin | Native | Out | Override | Private |
		Property | Protected | Public | Return | Static | Struct | Try | Type |
		TypeOf | Union | Unsafe | Until | Virtual | While | Yield
	}

	Abstract = { "abstract" }
	Break = { "break" }
	Catch = { "catch" }
	Ceval = { "ceval" }
	Class = { "class" }
	Const = { "const" }
	Continue = { "continue" }
	Defer = { "defer" }
	Do = { "do" }
	Else = { "else" }
	Enum = { "enum" }
	Final = { "final" }
	Finally = { "finally" }
	For = { "for" }
	ForEach = { "foreach" }
	If = { "if" }
	In = { "in" }
	Interface = { "interface" }
	Is = { "is" }
	Let = { "let" }
	Loop = { "loop" }
	Macro = { "macro" }
	Mixin = { "mixin" }
	Native = { "native" }
	Out = { "out" }
	Override = { "override" }
	Private = { "private" }
	Property = { "property" }
	Protected = { "protected" }
	Public = { "public" }
	Return = { "return" }
	Static = { "static" }
	Struct = { "struct" }
	Try = { "try" }
	Type = { "type" }
	TypeOf = { "typeof" }
	Union = { "union" }
	Unsafe = { "unsafe" }
	Until = { "until" }
	Virtual = { "virtual" }
	While = { "while" }
	Yield = { "yield" }

// Glyphs //////////////////////////////////////////////////////////////////////

	Glyph = _{
		BackTick | Tilde | Exclamation | At | Pound | Dollar | Percent | Caret |
		Ampersand | Asterisk | LeftParen | RightParen | Minus | Underscore |
		Plus | Equals | VertLine | BackSlash | LeftBracket | RightBracket |
		LeftBrace | RightBrace | Colon | Semicolon | Comma | Period | LeftAngle |
		RightAngle | Question | ForwardSlash
	}

	BackTick = { "`" }
	Tilde = { "~" }
	Exclamation = { "!" }
	At = { "@" }
	Pound = { "#" }
	Dollar = { "$" }
	Percent = { "%" }
	Caret = { "^" }
	Ampersand = { "&" }
	Asterisk = { "*" }
	LeftParen = { "(" }
	RightParen = { ")" }
	Minus = { "-" }
	Underscore = { "_" }
	Plus = { "+" }
	Equals = { "=" }
	VertLine = { "|" }
	BackSlash = { "\\" }
	LeftBracket = { "[" }
	RightBracket = { "]" }
	LeftBrace = { "{" }
	RightBrace = { "}" }
	Colon = { ":" }
	Semicolon = { ";" }
	Comma = { "," }
	Period = { "." }
	LeftAngle = { "<" }
	RightAngle = { ">" }
	Question = { "?" }
	ForwardSlash = { "/" }

// Miscellaneous ///////////////////////////////////////////////////////////////

	Ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
